/* Registra N cliques do mouse e, caso estejam
   dentro de um retângulo, marca a posição e
   traça uma linha reta conectando os cliques */
programa n_cliques;
inclui "gra*";
inclui "evnt*";

const
  X1 = 10; Y1 = 10; DX = 300; DY = 300;
  N = 20;

var
  x1, y1, x2, y2, i: inteiro;
  clicou_fora: lógico;

início
  /* desenha o retângulo grande */
  dsn_ret(tela, X1, Y1, DX, DY);

  /* considera um primeiro clique em X1, Y1 */
  x2 := X1; y2 := Y1;
  alt_pos_caneta(tela, x2, y2);

  /* registra N cliques */
  para (i := 0; i < N; i++)
  início
    /* aguarde o clique e obtenha as coordenadas */
    espere_por(EV_CLIQUE);
    mouse_xy(x1, y1);

    /* verifique se foi fora do retângulo */
    clicou_fora := (x1 < X1) || (y1 < Y1) ||
                   (x1 > X1 + DX) || (y1 > Y1 + DY);
    se (clicou_fora)
      continue; // descarta clique fora do retângulo

    /* desenha a marca e traça a linha */
    dsn_ret(tela, x1 - 3, y1 - 3, 2 * 3, 2 * 3);
    dsn_lin(tela, x1, y1);

    /* guarda as coordenadas anteriores */
    x2 := x1;
    y2 := y1;
  fim;
fim.
