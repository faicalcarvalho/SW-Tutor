/* Gera arquivo com os caracteres invertidos */
programa inverte_arq;
inclui "arq*";

/* txt_invertido - retorna texto com os caracteres 
   na ordem inversa à de txt */
função txt_invertido(txt: texto): texto;
var
  i: inteiro;
  result: texto;
início
  result := "";
  para (i := tamanho(txt) - 1; i >= 0; i--)
    result := result + caractere_em(txt, i);
  retorne result;
fim;

const
  ARQ1 = "regular.txt";
  ARQ2 = "invertido.txt";
  T = "ANOTARAM A DATA DA MARATONA";

var
  t: texto;

início
  /* se não existe, cria o arquivo 
     colocando nele o texto T */
  se (não existe_arq(ARQ1))
    txt_para_arq(ARQ1, T);

  /* lê o arquivo ARQ1, inverte o texto 
     e grava em ARQ2 */
  t := arq_para_txt(ARQ1);
  escreva(t);
  t := txt_invertido(t);
  escreva("\n" + t);
  txt_para_arq(ARQ2, t);
fim.
