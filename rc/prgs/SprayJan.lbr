/* Desenho livre com o mouse, através de pontos
   (pixels) escolhidos aleatoriamente seguindo
   o movimento do mouse pressionado */
programa Spray;
inclui "gra*";
inclui "evnt*";
inclui "mat*";

/* ptPerto - retorna um ponto próximo a (x, y) */
procedimento ptPerto(x, y: inteiro; 
                     var xp, yp: inteiro);
const
  RAIO = 20;
var
  raio: inteiro;
início
  raio := random(RAIO);
  se (random(2) = 0)
    raio := -raio; // inverte, talvez
  xp := x + raio;
  raio := random(RAIO);
  se (random(2) = 0)
    raio := -raio; // inverte, talvez
  yp := y + raio;
fim;

/* desenha NUM_PONTOS ao redor de (x, y) */
procedimento spray(x, y, cor: inteiro);
const
  NUM_PONTOS = 50;
var
  i, xp, yp: inteiro;
início
  para (i := 0; i < NUM_PONTOS; inc(i))
  início
    ptPerto(x, y, xp, yp);
    altPixel(Tela, xp, yp, cor);
  fim;
fim;

início
  /* aguarda o EV_MOUSE_BAIXO */
  esperePor(EV_MOUSE_BAIXO);

  /* desenha */  
  repita
    /* trata o movimento com o botão apertado */
    esperePor(EV_MOUSE_CIMA + EV_MOV_MOUSE);

    /* aperta o spray */
    spray(mouseX(), mouseY(), COR_VERMELHO);
  até (últimoEvento() = EV_MOUSE_CIMA);
fim.
